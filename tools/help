function help_constructor {
	grab messages zsh/parameter
}

function help_destructor {
	unset -f help usage
}

function help_help {
	retval=( "Display this help text" )
}

function help {
	typeset -i printf_help_offset=20
	echo "${fg_bold[green]}Global options:${reset_color}"
	help_help
	printf "  %s, %-$(( $printf_help_offset - 4 ))s %s\n" "-h" "--help" "$retval"
	echo
	echo "${fg_bold[green]}Available tools:${reset_color}"

	local key REPLY
	all_in_path box_path
	typeset -A box_path_elems
	box_path_elems=( ${(kv)retval_kv} )
	for key in ${(k)box_path_elems}; do
		[[ ! -x $box_path_elems[$key] ]] && continue
		grab "$key"
		"${key}_help"
		while read -r; do
			printf "  %-${printf_help_offset}s %s\n" "$key" "$REPLY"
			key=""
		done <<< "$retval"
	done
}

function usage {
	echo "Usage: $PROGNAME [global options] <tool> [module options]"
}

# vim:ft=zsh
