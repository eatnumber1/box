function help_constructor {
	grab messages zsh/parameter
}

function help_destructor {
	unset -f help _help_all_in_path usage
}

function help_help {
	retval=( "Display this help text" )
}

function _help_all_in_path {
	assert "[[ $# -eq 1 ]]"
	typeset -a path
	typeset -A saved_commands
	saved_commands=( ${(kv)commands} )
	path=( ${${(P)1}[@]} )
	hash -f
	retval_kv=( ${(kv)commands[@]} )
	commands=( ${(kv)saved_commands} )
}

function help {
	typeset -i printf_help_offset=20
	echo "${fg_bold[green]}Global options:${reset_color}"
	help_help
	printf "  %s, %-$(( $printf_help_offset - 4 ))s %s\n" "-h" "--help" "$retval"
	echo
	echo "${fg_bold[green]}Available tools:${reset_color}"

	local key REPLY
	_help_all_in_path box_path
	for key in ${(k)retval_kv[@]}; do
		[[ ! -x ${retval_kv[$key]} ]] && continue
		grab "$key"
		"${key}_help"
		while read -r; do
			printf "  %-${printf_help_offset}s %s\n" "$key" "$REPLY"
			key=""
		done <<< "$retval"
	done
}

function usage {
	echo "Usage: $PROGNAME [global options] <tool> [module options]"
}

# vim:ft=zsh
