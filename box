#!/bin/zsh
emulate -L zsh
setopt err_exit
#setopt xtrace

# TODO: Treat zshmodules as tools
zmodload zsh/parameter

# TODO: Add zparseopts support

typeset -r PROGNAME="${0##*/}"

typeset -a box_path
typeset -r box_userdir="$HOME/.box"
if [[ -d "$box_userdir" ]]; then
	[[ -f "$box_userdir/config" ]] && source "$box_userdir/config"
	[[ -d "$box_userdir/tools" ]] && box_path+=( "$box_userdir/tools" )
fi

# Boostrap functions: These will be replaced once we are fully initialized.
function assert {
	if ! eval "$*"; then
		echo "Assertion failed at $functrace[1]" >&2
		exit 255
	fi
}
# End bootstrap functions

typeset -ga loaded_tools
typeset -ga box_tool_destructors

function box_path_search_one {
	assert "[[ $# == 2 ]]"

	local i
	for i in $2/*; do
		if [[ ${i##*/} == $1 ]]; then
			retval=( "$i" )
			return 0
		fi
	done
	return 1
}

function box_path_search {
	assert "[[ $# == 1 ]]"
	assert "(( $+box_path ))"
	
	local path_elem
	for path_elem in "$box_path[@]"; do
		box_path_search_one "$1" "$path_elem" || continue

		retval=( "$path_elem" )
		return 0
	done
	return 1
}

function box_tool_constructor {
	assert "[[ $# -eq 2 ]]"
	local modname="$1" moddir="$2"
	local modfile="$moddir/$modname"
	assert "[[ -f $modfile ]]"

	source "$modfile"
	local fname="_box_${modname}_destructor"
	local cleanup="function $fname {; "
	(( ${+functions[${modname}_constructor]} )) && cleanup+="unset -f ${modname}_constructor; "
	if (( ${+functions[${modname}_destructor]} )); then
		cleanup+="${modname}_destructor; "
		cleanup+="unset -f ${modname}_destructor; "
	fi
	cleanup+="unset -f $fname; }"
	eval "$cleanup"
	loaded_tools+=( "$modname" )
	box_tool_destructors+=( "$fname" )
	(( ${+functions[${modname}_constructor]} )) && ${modname}_constructor
	return 0
}

function box_tool_loaded {
	assert "[[ $# -eq 1 ]]"
	(( ${+loaded_tools[(r)*${1}]} ))
}

function grab {
	assert "[[ $# -ne 0 ]]"

	local tool
	for tool in "$@"; do
		[[ -z "$tool" ]] && continue
		box_tool_loaded "$tool" && continue
		box_path_search "$tool" || return 1
		
		box_tool_constructor "$tool" "$retval"
	done
}

function box_tool_destructor {
	assert "[[ $# -eq 1 ]]"
	
	$box_tool_destructors[$1]
	box_tool_destructors[$1]=()
	loaded_tools[$1]=()
}

# Always grab assert first because box_tool_destructor uses it
grab assert

grab usage
if ! grab "$1"; then
	usage
	exit 1
fi
"$@"

typeset -i i
for (( i = $#box_tool_destructors; i > 0; i-- )); do
	box_tool_destructor $i
done
unset loaded_tools i
