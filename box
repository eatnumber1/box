#!/bin/zsh
emulate -L zsh
setopt err_exit
#setopt xtrace

# TODO: Add zparseopts support

typeset -r PROGNAME="${0##*/}"

typeset -a box_path
typeset -r box_userdir="$HOME/.box"
if [[ -d "$box_userdir" ]]; then
	[[ -f "$box_userdir/config" ]] && source "$box_userdir/config"
	[[ -d "$box_userdir/tools" ]] && box_path+=( "$box_userdir/tools" )
fi

# Boostrap functions: These will be replaced once we are fully initialized.
function assert {
	if ! eval "$*"; then
		echo "Assertion failed while grabbing assert" >&2
		exit 255
	fi
}
# End bootstrap functions

typeset -ga loaded_tools
typeset -ga box_tool_destructors

function box_path_search_one {
	assert "[[ $# == 2 ]]"

	local i
	for i in $2/*; do
		if [[ ${i##*/} == $1 ]]; then
			retval=( "$i" )
			return 0
		fi
	done
	return 1
}

function box_path_search {
	assert "[[ $# == 1 ]]"
	assert "(( $+box_path ))"
	
	local path_elem
	for path_elem in "$box_path[@]"; do
		box_path_search_one "$1" "$path_elem" || continue

		retval=( "$path_elem" )
		return 0
	done
	return 1
}

function box_is_defined {
	assert "[[ $# -eq 1 ]]"
	(( ${+functions[$1]} ))
}

function box_run_if_defined {
	assert "[[ $# -eq 1 ]]"
	box_is_defined "$1" && "$1"
}

function box_tool_constructor {
	assert "[[ $# -eq 2 ]]"
	local modname="$1" moddir="$2"
	local modfile="$moddir/$modname"
	assert "[[ -f $modfile ]]"

	source "$modfile"
	loaded_tools+=( "$modname" )
	box_run_if_defined "${modname}_constructor"
	local fname="_box_${modname}_destructor"
	local cleanup="function $fname {; "
	box_is_defined "${modname}_constructor" && cleanup+="unset -f ${modname}_constructor; "
	if box_is_defined "${modname}_destructor"; then
		cleanup+="${modname}_destructor; "
		cleanup+="unset -f ${modname}_destructor; "
	fi
	cleanup+="unset -f $fname; }"
	eval "$cleanup"
	box_tool_destructors+=( "$fname" )
	return 0
}

function box_tool_loaded {
	assert "[[ $# -eq 1 ]]"
	(( ${+loaded_tools[(r)*${1}]} ))
}

function box_zmodload {
	assert "[[ $# -eq 1 ]]"

	local modname="$1"
	zmodload "$modname" 2>/dev/null || return 1
	local fname="_box_${modname}_destructor"
	eval "function $fname {
		zmodload -u $modname
		unset -f $fname
	}"
	box_tool_destructors+=( "$fname" )
	loaded_tools+="$modname"
}

function grab {
	assert "[[ $# -ne 0 ]]"

	local tool
	for tool in "$@"; do
		[[ -z "$tool" ]] && continue
		box_tool_loaded "$tool" && continue
		if ! box_path_search "$tool"; then
			box_zmodload "$tool" || return 1
		else
			box_tool_constructor "$tool" "$retval"
		fi
	done
}

function box_tool_destructor {
	# Can't call assert here because this is called late enough that assert
	# might have been undefined already
	
	$box_tool_destructors[$1]
	box_tool_destructors[$1]=()
	loaded_tools[$1]=()
}

# Always grab assert first because box_tool_destructor uses it
grab assert

if [[ $PROGNAME == "box" ]]; then
	grab usage
	if ! grab "$1"; then
		usage
		exit 1
	fi
	"$@"
else
	grab $0(:t)
	$0(:t) "$@"
fi

typeset -i i
for (( i = $#box_tool_destructors; i > 0; i-- )); do
	box_tool_destructor $i
done
unset loaded_tools box_tool_destructors i
